plugins {
    id 'java'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

group = 'fr.farmeurimmo'
version = '1.0.0'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(24))
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.minestom:minestom:2025.09.13-1.21.8'
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.name
            version = project.version

            pom {
                name = 'MinestomFastViews'
                description = 'A simple and powerful Minestom inventory GUI wrapper'
                url = 'https://github.com/Farmeurimmo/MinestomFastViews'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'Farmeurimmo'
                        name = 'Farmeurimmo'
                        email = 'contact@farmeurimmo.fr'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/Farmeurimmo/MinestomFastViews.git'
                    developerConnection = 'scm:git:ssh://github.com:Farmeurimmo/MinestomFastViews.git'
                    url = 'https://github.com/Farmeurimmo/MinestomFastViews/tree/master'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}